plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk sdk_compile

    namespace "uk.co.itmms.androidArchitecture"

    defaultConfig {
        applicationId "uk.co.itmms.androidArchitecture"
        minSdk sdk_min
        targetSdk sdk_target
        versionCode version_code
        versionName version_name

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.2"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1,LICENSE*.md}'
        }
    }
    hilt {
        enableAggregatingTask true
    }
    kapt {
        correctErrorTypes true
    }
}

dependencies {
    implementation project(path: ':domain')
    implementation project(path: ':data')

    implementation 'androidx.core:core-ktx:1.9.0'

    // Compose
    def composeBom = platform("androidx.compose:compose-bom:2023.01.00")
    implementation composeBom
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.material:material")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.runtime:runtime-livedata")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1")
    implementation("androidx.activity:activity-compose:1.6.1")
    debugImplementation("androidx.compose.ui:ui-tooling")

    // Multidex support
    implementation 'androidx.multidex:multidex:2.0.1'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.5.3'

    // ********** Testing **********

    // Android
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // JUnit
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // MockK
    def mockk_version = '1.13.4'
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"

    // Coroutines
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Compose
    androidTestImplementation composeBom
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    debugImplementation("androidx.compose.ui:ui-test-manifest")

    // Hilt
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_version"
    androidTestImplementation  "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"
}