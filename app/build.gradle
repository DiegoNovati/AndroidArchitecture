plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    compileSdk sdk_compile

    namespace "uk.co.itmms.androidArchitecture"

    defaultConfig {
        applicationId "uk.co.itmms.androidArchitecture"
        minSdk sdk_min
        targetSdk sdk_target
        versionCode version_code
        versionName version_name

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = '21'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.2"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1,LICENSE*.md}'
        }
    }
    hilt {
        enableAggregatingTask true
    }
    kapt {
        correctErrorTypes true
    }
}

dependencies {
    implementation project(path: ':domain')
    implementation project(path: ':data')

    implementation 'androidx.core:core-ktx:1.15.0'

    // Compose
    implementation platform('androidx.compose:compose-bom:2024.11.00')
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.material:material")
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.runtime:runtime-livedata")
    implementation('androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7')
    implementation('androidx.activity:activity-compose:1.9.3')
    debugImplementation("androidx.compose.ui:ui-tooling")

    // Multidex support
    implementation 'androidx.multidex:multidex:2.0.1'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.8.4'

    // ********** Testing **********

    // Android
    testImplementation "androidx.arch.core:core-testing:$core_testing_version"

    // JUnit
    testImplementation "junit:junit:$junit_version"

    // MockK
    testImplementation "io.mockk:mockk:$mockk_version"

    // Coroutines
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Hilt
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_version"

    // Compose
    debugImplementation("androidx.compose.ui:ui-test-manifest")

    // ********** Android Testing **********

    // JUnit
    androidTestImplementation "androidx.test.ext:junit:$junit_ext_version"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"

    // MockK
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"

    // Hilt
    androidTestImplementation  "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"

    // Compose
    androidTestImplementation platform('androidx.compose:compose-bom:2024.11.00')
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
}